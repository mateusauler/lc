Prog -> {DecVar|DecConst} main BlocoCmd EOF

DecVar -> (int | char | boolean) Var {, Var} ;
DecConst -> final ID = [+|-] CONST ;

Var -> ID [:= [+|-] CONST | "[" CONST "]" ]

BlocoCmd -> "{" {CmdT} "}"

CmdS -> ID [ "[" Exp "]" ] := Exp
CmdS -> readln "(" ID [ "[" Exp "]" ] ")"
CmdS -> (write | writeln) "(" Exp {, Exp} ")"

CmdFor -> for "(" [CmdS {, CmdS}] ; Exp ; [CmdS {, CmdS}] ")" (CmdT | BlocoCmd)
CmdIf -> if "(" Exp ")" then (CmdT | BlocoCmd) [else (CmdT | BlocoCmd)]

CmdT -> [CmdS] ; | CmdFor | CmdIf

Exp -> Soma [(=|<>|>|<|>=|<=) Soma]
Soma -> [+|-] Termo {(+|-|or) Termo}
Termo -> Fator {(*|/|%|and) Fator}
Fator -> not Fator | "(" Exp ")" | ID [ "[" Exp "]" ] | CONST
