Prog -> {DecVar|DecConst} main BlocoCmd EOF

DecVar -> (int | char | boolean) ID [:= [+|-] CONST | "[" CONST "]" ] {, ID [:= [+|-] CONST | "[" CONST "]" ]} ;
DecConst -> final ID = [+|-] CONST ;

BlocoCmd -> "{" {CmdT} "}"

CmdS -> ID [ "[" Exp "]" ] := Exp
CmdS -> readln "(" ID [ "[" Exp "]" ] ")"
CmdS -> write "(" Exp {, Exp} ")"
CmdS -> writeln "(" Exp {, Exp} ")"

CmdFor -> for "(" [CmdS {, CmdS}] ; Exp ; [CmdS {, CmdS}] ")" (CmdT | BlocoCmd)
CmdIf -> if "(" Exp ")" then (CmdT | BlocoCmd) [else (CmdT | BlocoCmd)]

CmdT -> [CmdS] ; | CmdFor | CmdIf

Exp -> Soma [(=|<>|>|<|>=|<=) Soma]
Soma -> [+|-] Termo {(+|-|or) Termo}
Termo -> Fator {(*|/|%|and) Fator}
Fator -> not Fator | "(" Exp ")" | ID [ "[" Exp "]" ] | CONST
